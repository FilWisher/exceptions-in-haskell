\chapter{CDC Interpreter}

This chapter explores the implementation of an interpreter for CDC. Portions of source code are examined in detail although the full source can be found in the appendix.

\section{Implementation}

Although Peyton-Jones \emph{et al.} implement a language-level module for CDC, 
we are interested in the intermediate term transformations.
Using the step-by-step transformations produced by this interpreter,
we can construct and verify the implementations of \ltry\ and \lmu\ into CDC.
Examining transformation steps in full also allows us to derive proofs of soundness and completeness for these translations. 
For this reason, the interpreter was implemented as a term-rewriting program.

We follow the operational semantics of the system to provide an implementation.
This is not necessary and results in an ineffecient implementation.
Despite this, it is the simplest approach to implementation and efficiency is not central to producing proofs.

\subsection{Data structures}
There are two data types for representing CDC terms, \mono{Value} and \mono{Expr}
(Figure \ref{fig:cdc-data-structures}).
Values are not evaluated: when a term has been reduced to a value, 
it has terminated on that value. 
An expression (\mono{Expr}) is a term that can be evaluated to another term. 
The only exception is a \mono{Hole} which can take any position an expression can. 
For this reason, it must be a data constructor for expression types.

The core of the abstract machine is a function from one state to the next. 
A state is its own data type which corresponds to the tuple from the specification of the semantics of the abstract machine $\langle e,\ D,\ E,\ q\rangle$.

\begin{figure}\label{fig:cdc-data-structures}
\Verbatimcode
data Value = Var Char
  | Abs Char Expr
  | Prompt Int
  | Seq [Expr]
  deriving (Show, Eq)
  
data Expr = Val Value 
  | App Expr Expr
  | Hole
  | PushPrompt Expr Expr
  | PushSubCont Expr Expr
  | WithSubCont Expr Expr
  | NewPrompt
  
  | Sub Expr Expr Char
  deriving (Show, Eq)

data State = State Expr Expr [Expr] Value
  deriving (Show, Eq)
\end{Verbatim}
\caption{Data structures for the CDC interpreter}
\end{figure}

\subsection{Utility Functions}
Some utility functions are simplify the implementation.
Informally, these functions behave as follows (see Figure ~\ref{fig:cdc-helper-functions} for implementations details):

\begin{itemize}\itemsep0.1cm

\item \mono{prettify :: Expr -> String} is defined inductively for pretty-\\ printing terms.

\item \mono{ret :: Expr -> Expr -> Expr} returns the first expression with any holes filled in by the second expression.

\item \mono{composeContexts :: [Expr] -> Expr} takes a sequence of expressions and, starting from the end, 
fills the hole of each expression with the previous expression. 
This in effect joins the output of each context with the input of the next context.

\item \mono{promptMatch :: Int -> Expr -> Bool} returns true if the second argument is a Prompt with the same value as the first argument

\item \mono{splitBefore :: [Expr] -> Int -> [Expr]} returns the sequence of expressions up until (but not including) the prompt matching the second argument.

\item \mono{splitAfter :: [Expr] -> Int -> [Expr]} returns the sequence of expressions from (but not including) the prompt matching the second argument.

\item \mono{sub :: Expr -> Expr -> Char -> Expr} returns the first expression with all occurences of the third expression replaced by the second expression. 
If we name the arguments \mono{sub M V x} then this corresponds to the result of evaluating the substitution notation $M[v/x]$.
\end{itemize}

\begin{figure}\label{fig:cdc-helper-functions}
\Verbatimcode
  ret :: Expr -> Expr -> Expr
  ret d e = case d of
    Hole -> e
    App m n -> App (ret m e) (ret n e)
    Val (Abs x m) -> Val $ Abs x (ret m e)
    PushPrompt m n -> PushPrompt (ret m e) (ret n e)
    WithSubCont m n -> WithSubCont (ret m e) (ret n e)
    PushSubCont m n -> PushSubCont (ret m e) (ret n e)
    otherwise -> d

  composeContexts :: [Expr] -> Expr
  composeContexts = foldr ret Hole . reverse

  sub :: Expr -> Expr -> Char -> Expr
  sub m v x = case m of
    Val (Var n) -> if n == x then v else m
    Val (Abs y e) -> Val (Abs y $ sub e v x)
    Val (Prompt p) -> Val (Prompt p)
    App e e' -> App (sub e v x) (sub e' v x)
    NewPrompt -> NewPrompt
    PushPrompt e e' -> PushPrompt (sub e v x) (sub e' v x)
    WithSubCont e e' -> WithSubCont (sub e v x) (sub e' v x)
    PushSubCont e e' -> PushSubCont (sub e v x) (sub e' v x)

  promptMatch :: Int -> Expr -> Bool
  promptMatch i p = case p of
    (Val (Prompt p')) -> i == p'
    otherwise -> False
 
  splitBefore :: [Expr] -> [Expr]
  splitBefore p es = takeWhile (not . promptMatch p) es

  splitAfter :: [Expr] -> [Expr]
  splitAfter  p es = case length es of
    0 -> []
    otherwise -> tail list
    where list = dropWhile (not . promptMatch p) es
\end{Verbatim}
\caption{Utility functions for CDC interpreter}
\end{figure}

% TODO: make this section a little more structured
\subsection{Reduction Rules}

The heavy lifting of the interpreter is done by the function \mono{eval :: State -> State}. 
\mono{eval} is defined inductively on the structure of CDC terms. 
Using pattern-matching, each case of \mono{eval} corresponds directly to at least one of the reduction rules of the CDC abstract machine. 

\subsubsection{Application}
{
\relscale{0.9}
\[
\begin{array}{lcll}
\langle e\ e^\prime, D, E, q \rangle &\to &\langle e, D[\square\ e^\prime], E, q \rangle &\text{e non-value} \\
\langle v\ e, D, E, q \rangle &\to &\langle e, D[v\ \square], E, q \rangle &\text{e non-value} \\
\langle (`lx.e)\ v, D, E, q \rangle &\to &\langle e[v/x], D, E, q \rangle \\
\end{array}
\]
}
The \mono{App e e'} case deals with applications:
if both terms are values and the first term is an abstraction of the form \mono{$`l$x.m}, 
the dominant term becomes a substitution of \mono{e'} for \mono{x} in \mono{m}. 
Otherwise, the term that is not a value is made the dominant term and the remainder of the application is added to the current context. 
If both terms are redexes, the left-most is made the dominant term first. 
In effect, an application first ensures the left-hand term has been evaluated fully before evaluating the right-hand term.
\Verbatimcode
eval (State (App e e') d es q) = case e of
  Val v -> case e' of 
    Val _ -> case v of 
      Abs x m -> State (Sub m e' x) d es q
      Seq es' -> State (ret (composeContexts es') e') d es q
      otherwise -> State (App e e') d es q
    otherwise -> State e' (ret d (App e Hole)) es q
  otherwise -> State e (ret d (App Hole e')) es q
\end{Verbatim}

\subsubsection{PushPrompt}
{
\relscale{0.9}
\[
\begin{array}{lcll}
\langle pushPrompt\ e\ e^\prime, D, E, q \rangle &\to &\langle e, D[pushPrompt\ \square\ e^\prime], E, q \rangle &\text{e non-value} \\
\langle pushPrompt\ p\ e, D, E, q \rangle &\to &\langle e, \square, p : D : E, q \rangle \\
\end{array}
\]
}

The \mono{PushPrompt e e'} case ensures the left term is a value.
It then pushes the first argument (a prompt) and the current context onto the stack and makes the second argument the dominant term. 
\Verbatimcode
eval (State (PushPrompt e e') d es q) = case e of
  Val _ -> State e' Hole (e:d:es) q
  otherwise -> case d of
    Hole -> State e (PushPrompt Hole e') es q
    otherwise -> State e (ret d (PushPrompt Hole e')) es q
\end{Verbatim}

\subsubsection{WithSubCont}
{
\relscale{0.9}
\[
\begin{array}{lcll}
\langle withSubCont\ e\ e^\prime, D, E, q \rangle &\to &\langle e, D[withSubCont\ \square\ e^\prime], E, q \rangle &\text{e non-value} \\
\langle withSubCont\ p\ e, D, E, q \rangle &\to &\langle e, D[withSubCont\ p\ \square], E, q \rangle &\text{e non-value} \\
\langle withSubCont \ p\ v, D, E, q \rangle &\to &\langle v (D : E\until{p}, \square, E\from{p}, q \rangle \\
\end{array}
\]
}

The reduction rules for \mono{WithSubCont e e'} ensure that the first argument has been evaluated to a prompt \mono{p} and then that the second argument has been evaluated to an abstraction. 
Finally, it appends the current continuation to the sequence yielded by splitting the continuation stack at \mono{p}, 
and creates an application of the second argument to this sequence.
\Verbatimcode
eval (State (WithSubCont e e') d es q) = 
  case e of
    Val v -> case e' of
      Val _ -> case v of (Prompt p) -> 
        State (App e' (seq' (d:beforeP))) Hole afterP q
          where beforeP = splitBefore p es
                afterP = splitAfter p es
      otherwise -> State e' (ret d (WithSubCont e Hole)) es q 
    otherwise -> State e (ret d (WithSubCont Hole e')) es q 
\end{Verbatim}

\subsubsection{PushSubCont}
{
\relscale{0.9}
\[
\begin{array}{lcll}
\langle pushSubCont\ e\ e^\prime, D, E, q \rangle &\to &\langle e, D[pushSubCont\ \square\ e^\prime], E, q \rangle &\text{e non-value} \\
\langle pushSubCont E^\prime\ e, D, E, q \rangle &\to &\langle e, \square, E^\prime \app (D : E), q \rangle \\
\end{array}
\]
}
Reducing \mono{PushSubCont e e'} ensures that the first argument is a sequence.
Then it pushes the current continuation, followed by this sequence, onto the stack.
The second argument is promoted to be the dominant term.
This has the effect of evaluating the dominant term and return the result to the sequence.
\Verbatimcode
eval (State (PushSubCont e e') d es q) = 
  case e of
    Val (Seq es') -> State e' Hole (es'++(d:es)) q
    otherwise -> State e (ret d (PushSubCont Hole e')) es q
\end{Verbatim}

\subsubsection{Substitution}
The reduction of \mono{Sub e y x} uses \mono{sub} to recursively substitute the third argument with the second in the first. 
\Verbatimcode
eval (State (Sub e y x) d es q) = 
  State (sub e y x) d es q
\end{Verbatim}

\subsubsection{NewPrompt}
{
\relscale{0.9}
\[
\begin{array}{lcll}
\langle newPrompt, D, E, q \rangle &\to &\langle q, D, E, q+1 \rangle
\end{array}
\]
}
Evaluating \mono{NewPrompt} places the value of the current prompt as the dominant term and increments the global prompt counter:
\Verbatimcode
eval (State NewPrompt d es (Prompt p)) = 
  State (Val (Prompt p)) d es (Prompt $ p+1)
\end{Verbatim}

\subsection{Example}

The terms $pushPrompt$, $newPrompt$, $withSubCont$, and $pushSubCont$ are too long to work with easily.
We define the following aliases for convenience:
\definition{
\textsc{CDC aliases for operators}
\[
\begin{array}{rcl}
  \np & = & newPrompt \\
  \pp & = & pushPrompt \\
  \wsc & = & withSubCont \\
  \psc & = & pushSubCont \\
\end{array}
\]

Additionally, we have defined utility function aliases for our data constructors
\Verbatimcode
np = NewPrompt
pp = PushPrompt
wsc = WithSubCont
psc = PushSubCont
var = Val . Var 
abst c = Val . Abs c
\end{Verbatim}
}

We define an evaluation engine which recursively evaluates the term until the evaluation returns the same term.
At this point it terminates.
The evaluation engine takes a function of type \mono{State -> String} to specify the format of the printed term.

\Verbatimcode
evalFull :: (State -> String) -> State -> IO ()
evalFull formatFn st = do
  putStrLn $ formatFn st
  let st' = eval st in
    if st == st' then 
      putStrLn "QED"
    else 
      evalFull formatFn st'
\end{Verbatim}
We can translate the CDC term
\[
(`lp.\pp p\ (\wsc p\ (`l`a.\psc `a\ s))\ t)\ \np
\]
into the data structures defined by our interpreter:
\Verbatimcode
term = App 
  (abst 'p' 
    (pp (var 'p') (App 
      (wsc (var 'p') (abst 'a' 
        (psc (var 'a') (var 's')))) 
      (var 't')))) 
  np
\end{Verbatim}
Passing this state into our evaluator with \mono{prettifyState}, we get the following:

\begin{Verbatim}
(((\p.(pp p ((wsc p (\a.(psc a s)))t))) np), _, [], 0)
( np, ((\p.(pp p ((wsc p (\a.(psc a s)))t)))_), [], 0)
(0, ((\p.(pp p ((wsc p (\a.(psc a s)))t)))_), [], 1)
(((\p.(pp p ((wsc p (\a.(psc a s)))t)))0), _, [], 1)
((pp p ((wsc p (\a.(psc a s)))t))[0/p], _, [], 1)
((pp 0 ((wsc 0 (\a.(psc a s)))t)), _, [], 1)
(((wsc 0 (\a.(psc a s)))t), _, [0:_], 1)
((wsc 0 (\a.(psc a s))), (_t), [0:_], 1)
(((\a.(psc a s))(_t)), _, [_], 1)
((psc a s)[(_t)/a], _, [_], 1)
((psc (_t) s), _, [_], 1)
(s, _, [(_t):_:_], 1)
(s, (_t), [_:_], 1)
((st), _, [_:_], 1)
QED
\end{Verbatim}
From this, which we can translate to the following derivation:
{
\relscale{0.9}
\[
\begin{array}{lcrrr}
         & (`lp.\pp p\ (\wsc p\ (`l`a.\psc `a\ s))\ t)\ \np & \square & []                        & 0 \\
\tocdc\  & \np & (`lp \dots t)\ \square & []                                                      & 0 \\
\tocdc\  & 0 & (`lp \dots t)\ \square & []                                                        & 1 \\
\tocdc\  & (`lp.\pp p\ (\wsc p\ (`l`a.\psc `a\ s))\ t)\ 0 & \square    & []                        & 1 \\
\to_{`b} & (\pp p\ (\wsc p\ (`l`a.\psc `a\ s))\ t))[p/0]  & \square    & []                        & 1 \\
\to_{`b} & \pp 0\ (\wsc 0\ (`l`a.\psc `a\ s))\ t)         & \square    & []                        & 1 \\
\tocdc\  & (\wsc 0\ (`l`a.\psc `a\ s))\ t                 & \square    & 0:\square                 & 1 \\
\tocdc\  & \wsc 0\ (`l`a.\psc `a\ s))\                    & \square\ t & 0:\square                 & 1 \\
\tocdc\  & (`l`a.\psc `a\ s))(\square t)                  & \square    & \square                   & 1 \\
\to_{`b} & (\psc `a\ s)[\square t/`a]                     & \square    & \square                   & 1 \\
\to_{`b} & \psc \square t\ s                              & \square    & \square                   & 1 \\
\tocdc\  & s                                              & \square    & \square t:\square:\square & 1 \\
\tocdc\  & s                                              & \square\ t & \square:\square           & 1 \\
\tocdc\  & st                                             & \square    & \square:\square           & 1 \\
\end{array}
\]
}
